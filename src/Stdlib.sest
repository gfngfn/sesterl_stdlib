import Bool
import Binary
import Float
import Option
import Result
import List
import RawMap
import RawValue
import Timer
import ServerUtil
import StopReason
import GenServer
import Supervisor

module Stdlib :> sig

  type name<$a> =
    | Local(binary)
    | Global($a)

  module Bool : sig
    val not : fun(bool) -> bool
  end

  module Binary : sig
    val equal : fun(binary, binary) -> bool
    val from_list : fun(list<char>) -> binary
    val to_list : fun(binary) -> list<char>
    val to_list_sub : fun(binary, -pos int, -len int) -> option<list<char>>
    val at : fun(binary, -pos int) -> option<char>
  end

  module Float : sig
    val from_int : fun(int) -> float
    val round : fun(float) -> int
    val truncate : fun(float) -> int
  end

  module Option : sig
    val get<$a> : fun(option<$a>, -default $a) -> $a
    val get_or_invoke<$a> : fun(option<$a>, -default fun() -> $a) -> $a
    val map<$a, $b> : fun(fun($a) -> $b, option<$a>) -> option<$b>
    val bind<$a, $b> : fun(option<$a>, fun($a) -> option<$b>) -> option<$b>
  end

  module Result : sig
    val map<$a, $b, $e> : fun(fun($a) -> $b, result<$a, $e>) -> result<$b, $e>
    val map_error<$a, $e1, $e2> : fun(fun($e1) -> $e2, result<$a, $e1>) -> result<$a, $e2>
    val bind<$a, $b, $e> : fun(result<$a, $e>, fun($a) -> result<$b, $e>) -> result<$b, $e>
    val from_option<$a, $e> : fun(option<$a>, -error $e) -> result<$a, $e>
  end

  module List : sig
    val map<$a, $b> : fun(fun($a) -> $b, list<$a>) -> list<$b>
    val filter<$a> : fun(fun($a) -> bool, list<$a>) -> list<$a>
    val filter_map<$a, $b> : fun(fun($a) -> option<$b>, list<$a>) -> list<$b>
    val for_each<$msg, $a> : fun(fun($a) -> [$msg]unit, list<$a>) -> [$msg]unit
    val foldl<$a, $b> : fun(fun($b, $a) -> $b, $b, list<$a>) -> $b
    val foldl_effect<$m, $a, $b> : fun(fun($b, $a) -> [$m]$b, $b, list<$a>) -> [$m]$b
    val foldr<$a, $b> : fun(fun($a, $b) -> $b, $b, list<$a>) -> $b
    val all<$a> : fun(fun($a) -> bool, list<$a>) -> bool
    val any<$a> : fun(fun($a) -> bool, list<$a>) -> bool
    val append<$a> : fun(list<$a>, list<$a>) -> list<$a>
    val reverse<$a> : fun(list<$a>) -> list<$a>
    val length<$a> : fun(list<$a>) -> int
  end

  #[doc(```
    The wrapper module for Erlang’s `maps`.
    Care must be taken when using this module about the equality on types for keys;
    the module relies on the equality of Erlang values.
  ```)]
  module RawMap : sig
    type t :: (o, o) -> o
    val new<$k, $v> : fun() -> t<$k, $v>
    val put<$k, $v> : fun($k, $v, t<$k, $v>) -> t<$k, $v>
    val find<$k, $v> : fun($k, t<$k, $v>) -> option<$v>
    val remove<$k, $v> : fun($k, t<$k, $v>) -> t<$k, $v>
    val to_list<$k, $v> : fun(t<$k, $v>) -> list<{$k, $v}>
    val from_list<$k, $v> : fun(list<{$k, $v}>) -> t<$k, $v>
    val merge<$k, $v> : fun(t<$k, $v>, t<$k, $v>) -> t<$k, $v>
    val merge_with<$k, $v> : fun(fun($k, $v, $v) -> $v, t<$k, $v>, t<$k, $v>) -> t<$k, $v>
    val map<$k, $v1, $v2> : fun(fun($k, $v1) -> $v2, t<$k, $v1>) -> t<$k, $v2>
    val fold<$k, $v, $a> : fun(fun($k, $v, $a) -> $a, $a, t<$k, $v>) -> $a
  end

  module RawValue : sig
    #[doc(```
      The type for arbitrary Erlang values.
    ```)]
    type t :: o

    #[doc(```
      The type for decomposing Erlang values.
    ```)]
    type structure =
      | Int(int)
      | Float(float)
      | Atom(binary)
      | Binary(binary)
      | Tuple(list<t>)
      | Nil
      | Cons(t, t)
      | Map(list<{t, t}>)
      | Fun(binary)
      | Pid(binary)
      | Port(binary)
      | Ref(binary)
      | Other(binary)

    val show : fun(t) -> binary

    val lift : fun(t) -> structure

    #[doc(```
      Forgets the type of given values.
    ```)]
    val forget<$a> : fun($a) -> t
  end

  #[doc(```
    The wrapper module for Erlang’s `timer`.
  ```)]
  module Timer : sig
    type t :: o

    #[doc(```
      `send_after(duration, pid, msg)` sends the message `msg` to the process of PID `pid`
      after `duration` milliseconds.
    ```)]
    val send_after<$m, $a> : fun(int, pid<$a>, $a) -> [$m]result<t, RawValue.t>
    val send_after_self<$m> : fun(int, $m) -> [$m]result<t, RawValue.t>
    val cancel<$m> : fun(t) -> [$m]result<unit, RawValue.t>
  end

  module StopReason : sig
    type t :: o
    val normal : fun() -> t
    val shutdown : fun(option<RawValue.t>) -> t
    val abort : fun(RawValue.t) -> t

    type structure =
      | Normal
      | Shutdown(option<RawValue.t>)
      | Abort(RawValue.t)
      | Other(RawValue.t)

    val lift : fun(t) -> structure
  end

  #[doc(```
    The wrapper module for Erlang’s `gen_server`.
  ```)]
  module GenServer : sig

    type initialized :: (o) -> o

    val init_ok<$msg, $state> : fun($state) -> [$msg]initialized<$state>

    val init_stop<$msg, $state> : fun(StopReason.t) -> [$msg]initialized<$state>

    type reply :: (o, o, o) -> o

    val reply<$msg, $response, $state> :
      fun($response, $state, ?timeout int) -> [$msg]reply<$msg, $response, $state>

    val reply_and_stop<$msg, $response, $state> :
      fun(StopReason.t, $response, $state) -> [$msg]reply<$msg, $response, $state>

    type no_reply :: (o) -> o

    val no_reply<$msg, $state> :
      fun($state, ?timeout int) -> [$msg]no_reply<$state>

    val no_reply_and_stop<$msg, $state> :
      fun(StopReason.t, $state) -> [$msg]no_reply<$state>

    type start_link_error = RawValue.t

    type call_error = RawValue.t

    #[doc(```
      The signature required for callback modules of `GenServer`.
      Basically equivalent to Erlang’s `-behaviour(gen_server)`,
      but requires several types as well as callback functions.
    ```)]
    signature Behaviour = sig
      #[doc(```The type for arguments of `init`.```)]
      type init_arg :: o

      #[doc(```The type for synchronous messages sent to the server and handled by `handle_call`.```)]
      type request :: o  /* temporary; should be a unary GADT */

      #[doc(```The type for messages returned by the server in response to requests.```)]
      type response :: o  /* temporary; should be a unary GADT  */

      #[doc(```The type for asynchronous messages sent to the server and handled by `handle_cast`.```)]
      type cast_message :: o

      #[doc(```The type for general messages sent to the server and handled by `handle_info`.```)]
      type info :: o

      #[doc(```The type for states the server keeps.```)]
      type state :: o

      #[doc(````
        The type for suffixes of global registered names.
        For example, if you define:

        ```
        module Callback = struct
          ...
          type global = int
          ...
        end
        module SomeServer = GenServer.Make(Callback)
        ```

        then `SomeServer.start_link_name(..., -name Global(42))` will register the spawned process
        as `{some_server, 42}` globally.
        Specify any type (such as `unit`) as `global`
        if you will not use the global registration for your servers.
      ````)]
      type global :: o

      #[doc(```
        Equivalent to `gen_server`’s `Mod:init/1`.
      ```)]
      val init : fun(init_arg) -> [info]initialized<state>

      #[doc(```
        Equivalent to `gen_server`’s `Mod:handle_call/3`.
      ```)]
      val handle_call<$a> : fun(request, pid<$a>, state) -> [info]reply<info, response, state>

      #[doc(```
        Equivalent to `gen_server`’s `Mod:handle_cast/2`.
      ```)]
      val handle_cast : fun(cast_message, state) -> [info]no_reply<state>

      #[doc(```
        Equivalent to `gen_server`’s `Mod:handle_info/2`
        except that `timeout` will be received by `handle_timeout`.
      ```)]
      val handle_info : fun(info, state) -> [info]no_reply<state>

      #[doc(```
        Will be invoked when a timeout (that had been set in response to
        a return value of `handle_call`, `handle_cast`, `handle_info`, etc.)
        happens.
      ```)]
      val handle_timeout : fun(state) -> [info]no_reply<state>

      #[doc(```
        Equivalent to `gen_server`’s `Mod:terminate/1`.
      ```)]
      val terminate : fun(state) -> [info]unit
    end

    #[doc(```
      The functor that receives a module containing the callback functions and types
      and that produces a server module of the `gen_server` behaviour.
    ```)]
    module Make : fun(Callback : Behaviour) -> sig
      #[doc(```
        The type for server processes. Isomorphic to `pid<Callback.info>`.
      ```)]
      type proc :: o

      #[doc(```
        The conversion function from `proc` to `pid<Callback.info>`.
      ```)]
      val as_pid : fun(proc) -> pid<Callback.info>

      #[doc(```
        The reversed conversion function from `pid<Callback.info>` to `proc`.
      ```)]
      val from_pid : fun(pid<Callback.info>) -> proc

      #[doc(```
        Equivalent to `gen_server:call/{2,3}`.
      ```)]
      val call<$a> : fun(proc, Callback.request, ?timeout int) -> [$a]result<Callback.response, call_error>

      #[doc(```
        Equivalent to `gen_server:cast/2`.
      ```)]
      val cast<$a> : fun(proc, Callback.cast_message) -> [$a]unit

      #[doc(```
        Equivalent to `erlang:send/2`, but specialized for `proc`.
      ```)]
      val send_info<$a> : fun(proc, Callback.info) -> [$a]unit

      #[doc(```
        Equivalent to `gen_server:start_link/1`.
      ```)]
      val start_link<$a> : fun(Callback.init_arg) -> [$a]result<proc, start_link_error>

      #[doc(```
        Equivalent to `gen_server:start_link/2`.
      ```)]
      val start_link_name<$a> : fun(Callback.init_arg, -name name<Callback.global>) -> [$a]result<proc, start_link_error>

      val where_is_local<$a> : fun(binary) -> [$a]option<proc>

      val where_is_global<$a> : fun(Callback.global) -> [$a]option<proc>

      #[doc(```
        Equivalent to `gen_server:stop/1`.
      ```)]
      val stop<$a> : fun(proc) -> [$a]unit
    end
  end

  module Supervisor : sig
    type restart =
      | Permanent | Transient | Temporary

    type shutdown =
      | BrutalKill | Timeout(int) | Infinity

    type worker =
      | Worker | Supervisor

    module Static : sig
      type start_link_error = RawValue.t
      type child_proc :: o
      val make_child_proc<$a, $b, $c, $reason> : fun(fun() -> [$b]result<pid<$a>, $reason>) -> [$c]child_proc
      type child_spec :: (o, o) -> o
      val make_child_spec<$msg, $id> :
        fun(
          -id       $id,
          -start    frozen<unit, $msg, child_proc>,
          ?restart  restart,
          ?shutdown shutdown,
          ?type     worker,
        ) -> child_spec<$msg, $id>

      type strategy =
        | OneForAll | OneForOne | RestForOne

      type sup_flags :: o
      val make_sup_flags : fun(?strategy strategy, ?intensity int, ?period int) -> sup_flags
      type initialized :: (o, o) -> o
      val init_ok<$msg, $id> : fun(sup_flags, list<child_spec<$msg, $id>>) -> [$msg]initialized<$msg, $id>

      signature Behaviour = sig
        type child_id :: o
        type init_arg :: o
        type info :: o
        type global :: o
        val init : fun(init_arg) -> [info]initialized<info, child_id>
      end

      module Make : fun(Callback : Behaviour) -> sig
        type proc :: o
        val as_pid : fun(proc) -> pid<Callback.info>
        val from_pid : fun(pid<Callback.info>) -> proc
        val start_link<$a> : fun(Callback.init_arg) -> [$a]result<proc, start_link_error>
        val start_link_name<$a> : fun(Callback.init_arg, -name name<Callback.global>) -> [$a]result<proc, start_link_error>
        val where_is_local<$a> : fun(binary) -> [$a]option<proc>
        val where_is_global<$a> : fun(Callback.global) -> [$a]option<proc>
      end
    end

    module Dynamic : sig
      type start_link_error = RawValue.t
      type start_child_error = RawValue.t
      type terminate_child_error = RawValue.t
      type child_proc :: (o) -> o
      val make_child_proc<$b, $child_msg, $c, $reason> : fun(fun() -> [$b]result<pid<$child_msg>, $reason>) -> [$c]child_proc<$child_msg>
      type child_spec :: (o, o, o) -> o
      val make_child_spec<$msg, $arg, $child_msg> :
        fun(
          -start    frozen<{$arg}, $msg, child_proc<$child_msg>>,
          ?restart  restart,
          ?shutdown shutdown,
          ?type     worker,
        ) -> child_spec<$msg, $arg, $child_msg>
      type sup_flags :: o
      val make_sup_flags : fun(?intensity int, ?period int) -> sup_flags
      type initialized :: (o, o, o) -> o
      val init_ok<$msg, $arg, $child_msg> : fun(sup_flags, child_spec<$msg, $arg, $child_msg>) -> [$msg]initialized<$msg, $arg, $child_msg>

      signature Behaviour = sig
        type init_arg :: o
        type start_arg :: o
        type info :: o
        type child_info :: o
        type global :: o
        val init : fun(init_arg) -> [info]initialized<info, start_arg, child_info>
      end

      module Make : fun(Callback : Behaviour) -> sig
        type proc :: o
        val as_pid : fun(proc) -> pid<Callback.info>
        val from_pid : fun(pid<Callback.info>) -> proc
        val start_link<$a> : fun(Callback.init_arg) -> [$a]result<proc, start_link_error>
        val start_link_name<$a> : fun(Callback.init_arg, -name name<Callback.global>) -> [$a]result<proc, start_link_error>
        val where_is_local<$a> : fun(binary) -> [$a]option<proc>
        val where_is_global<$a> : fun(Callback.global) -> [$a]option<proc>
        val start_child<$a> : fun(proc, Callback.start_arg) -> [$a]result<pid<Callback.child_info>, start_child_error>
        val which_children<$a> : fun(proc) -> [$a]list<pid<Callback.child_info>>
        val terminate_child<$a> : fun(proc, pid<Callback.child_info>) -> [$a]result<unit, terminate_child_error>
      end
    end
  end

end = struct
  module Bool = Bool
  module Binary = Binary
  module Float = Float
  module Option = Option
  module Result = Result
  module List = List
  module RawMap = RawMap
  module RawValue = RawValue
  module Timer = Timer
  include ServerUtil
  module StopReason = StopReason
  module GenServer = GenServer
  module Supervisor = Supervisor
end
