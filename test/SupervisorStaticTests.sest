import RawValue
import Result
import Supervisor
import GenServer

module SupervisorStaticTests = #[test] struct

  module Sup :> sig
    type proc
    type start_error = Supervisor.Static.start_link_error
    val start_link<$a> : fun(unit) -> [$a]result<proc, start_error>
  end = struct

    type start_error = Supervisor.Static.start_link_error

    module Child1 = struct

      module Callback = struct

        type init_arg = int

        type request =
          | Get

        type response =
          | Got(int)

        type cast_message =
          | Set(int)

        type state = int

        type info = RawValue.t
        type global = unit
        type stop_reason = RawValue.t

        val init(n) = act
          GenServer.init_ok(n)

        val handle_call(request, from, n) = act
          case request of
          | Get -> GenServer.reply(Got(n), n)
          end

        val handle_cast(msg, n) = act
          case msg of
          | Set(m) -> GenServer.no_reply(m)
          end

        val handle_info(info, n) = act
          GenServer.no_reply(n)

        val terminate(n) = act
          return({})
      end

      include GenServer.Make(Callback)
    end

    module S = Supervisor.Static

    module SupCallback = struct

      type child_id = int
      type init_arg = unit

      type global = unit
      type info = RawValue.t
      type stop_reason = RawValue.t

      val start_child1(n) = act
        S.make_child_proc(fun() -> act
          do res <- Child1.start_link(n) in
          return(Result.map(Child1.as_pid, res))
        end)

      val init(init_arg) = act
        let sup_flags =
          S.make_sup_flags(
            ?strategy  S.OneForOne,
            ?intensity 1,
            ?period    5,
          )
        in
        let child_specs =
          [
            S.make_child_spec(
              -id    1,
              -start (freeze start_child1(42)),
            )
          ]
        in
        S.init_ok(sup_flags, child_specs)
    end

    include S.Make(SupCallback)
  end

  #[test]
  val main() =
    Testing.perform("static supervisor", fun() -> act
      do res <- Sup.start_link({}) in
      let opt =
        case res of
        | Error(reason) -> Error(reason)
        | Ok(proc)      -> Ok({})
        end
      in
      return(assert Testing.equal(-expect Ok({}), -got opt))
    end)

end
